#!/usr/local/bin/chomik

variable bribe price:integer;
let bribe price = value integer 10;

variable current person pays a bribe:code;
let current person pays a bribe = value code
{
    <subtract "integer" <points of <current person>> <bribe price>>;
    let points of <current person> = <the subtract result "integer">;    
        
    let variable the read from stream source stream index = <my function generator index>;
    <read from stream "function">;
    let variable function of <current person> = <the read from stream result "function">;    
    <print "you payed a bribe and became" <function of <current person>>>;
};


variable if we moved to Warszawa and have more than bribe price points then (A:compare_result) (B:compare_result):code;
let if we moved to Warszawa and have more than bribe price points then (A:compare_result) (B:compare_result) = value code {};

let if we moved to Warszawa and have more than bribe price points then equal equal = value code 
{
    <current person pays a bribe>;
};
let if we moved to Warszawa and have more than bribe price points then equal greater = value code 
{
    <current person pays a bribe>;
};


variable current person goto current place (X:compare_result):code;
let current person goto current place lower=value code {};
let current person goto current place greater=value code {};
let current person goto current place equal=value code 
{
    let location of <current person>=<current place>;
    
    <print <current person> "has moved to" <location of <current person>>>;
    
    # if we have moved to Warszawa and have more than 10 points then
    # 1. we get a new random function
    # 2. we pay all the points as a bribe
    <compare "place" <location of <current person>> Warszawa>;
    let we moved to Warszawa compare result = <the compare result>;
    
    <compare "integer" <points of <current person>> <bribe price>>;
    let we can afford the bribe compare result = <the compare result>;
    
    <if we moved to Warszawa and have more than bribe price points then 
            <we moved to Warszawa compare result> 
            <we can afford the bribe compare result>>;
        
    let decision of <current person>=value action no_action;    
};


variable current person tells current guest that current information (X:compare_result):code;
let current person tells current guest that current information lower = value code {};
let current person tells current guest that current information greater = value code {};
let current person tells current guest that current information equal = value code 
{
    <print <current person> "tells" <current guest> "that" <current information>>;
    
    let decision of <current person>=value action no_action;
};


variable current person asks current guest to current action (X:compare_result):code;
let current person asks current guest to current action lower = value code {};
let current person asks current guest to current action greater = value code {};
let current person asks current guest to current action equal = value code 
{
    execute <print <current person> "asks" <current guest> "to" <current action>>;
    
    let variable decision of <current person>=value action no_action;
};


variable current person asks current guest what is current guest 2 function (X:compare_result):code;
let current person asks current guest what is current guest 2 function lower = value code {};
let current person asks current guest what is current guest 2 function greater = value code {};
let current person asks current guest what is current guest 2 function equal = value code 
{
    <print <current person> "asks" <current guest> "what is" <current guest 2> "function">;
    
    let <current guest> thinks that <current person> has asked <current guest> what is <current guest 2> function=value boolean true;
    
    let variable decision of <current person>=value action no_action;
};


variable current person asks current guest what is current guest 2 weapon (X:compare_result):code;
let current person asks current guest what is current guest 2 weapon lower = value code {};
let current person asks current guest what is current guest 2 weapon greater = value code {};
let current person asks current guest what is current guest 2 weapon equal = value code 
{
    <print <current person> "asks" <current guest> "what is his weapon">;
    
    let <current guest> thinks that <current person> has asked <current guest> what is <current guest 2> weapon=value boolean true;
    
    let decision of <current person>=value action no_action;
};


variable current person asks current guest whether current guest 2 is a liar (X:compare_result):code;
let variable current person asks current guest whether current guest 2 is a liar lower = value code {};
let variable current person asks current guest whether current guest 2 is a liar greater = value code {};
let variable current person asks current guest whether current guest 2 is a liar equal = value code 
{
    execute <print <current person> "asks" <current guest> "whether" <current guest 2> "is a liar">;
    
    let <current guest> thinks that <current person> has asked <current guest> whether <current guest 2> is a liar = value boolean true;
        
    let variable decision of <current person>=value action no_action;
};

include "../chomik/notify_gotrek.chomik"
include "../chomik/resolve_the_events.chomik"




variable help the person:code;
let help the person = value code
{
    <add "integer" <efficiency of helpers> <strength of <current person who can help>>>;
    let helper bonus = <the add result "integer">;        
    let efficiency of helpers = <helper bonus>;
    <print <current person who can help> "is helping" <current person who is being helped> "adding" <helper bonus>>;
    
    <resolve the help>;
};


variable increase the efficiency of helpers 
        (HELPER_IS_DIFFERENT_THAN_HELPED:compare_result) 
        (HELPER_IS_DIFFERENT_THAN_ENEMY:compare_result) 
        (HELPER_CAN_SEE_THE_HELPED:boolean)
        (HELPER_HAS_THE_SAME_FUNCTION_AS_THE_HELPED:compare_result):code;
        
let increase the efficiency of helpers (HELPER_IS_DIFFERENT_THAN_HELPED:compare_result) 
        (HELPER_IS_DIFFERENT_THAN_ENEMY:compare_result) 
        (HELPER_CAN_SEE_THE_HELPED:boolean)
        (HELPER_HAS_THE_SAME_FUNCTION_AS_THE_HELPED:compare_result) = value code {};        
        
let increase the efficiency of helpers lower greater true equal = value code { <help the person>; };
let increase the efficiency of helpers greater greater true equal = value code { <help the person>; };
let increase the efficiency of helpers lower lower true equal = value code { <help the person>; };
let increase the efficiency of helpers greater lower true equal = value code { <help the person>; };






variable help the person from (H:person):code;
let help the person from (H:person)=value code
{
    let current person who can help = value person [(H:person)];
    
    # check that the helper is different from the person being helped
    <compare "person" <current person who is being helped> <current person who can help>>;            
    let information whether the helper is not the person being helped = <the compare result>;    
    
    # check that the helper is different from the person we are helping against
    <compare "person" <current person against whom we help> <current person who can help>>;
    let information whether the helper is not the person against whom we help = <the compare result>;
    
    # check that the helper can see the person being helped
    let information whether the helper can see the person being helped = <<current person who can help> can see <current person who is being helped>>;    
    
    # check that the function of the helper is the same as the function of the person being helped
    <compare "function" <function of <current person who is being helped>> <function of <current person who can help>>>;    
    let information whether the helper has the same function as the person being helped = <the compare result>;
        
        
    <print "for" <current person who is being helped> "and" <current person against whom we help> 
        "and helper" <current person who can help> "the informations are:"
        <information whether the helper is not the person being helped> " "
        <information whether the helper is not the person against whom we help> " "
        <information whether the helper can see the person being helped> " "
        <information whether the helper has the same function as the person being helped> >;
        
    <increase the efficiency of helpers 
        <information whether the helper is not the person being helped> 
        <information whether the helper is not the person against whom we help>
        <information whether the helper can see the person being helped> 
        <information whether the helper has the same function as the person being helped>>;
    
};



variable calculate the efficiency of the (P:person) helpers against (Q:person):code;
let calculate the efficiency of the (P:person) helpers against (Q:person)=value code
{
    let current person who is being helped = value person [(P:person)];
    let current person against whom we help = value person [(Q:person)];
    
    let efficiency of helpers = value integer 0;
        
    <help the person from (HELPER:person)>;
};



variable current person attacks current guest (X:compare_result):code;
let variable current person attacks current guest lower = value code {};
let variable current person attacks current guest greater = value code {};
let variable current person attacks current guest equal = value code 
{
    <print <current person> "attacks" <current guest>>;
    
    
    
    # calculate the efficiency of both fighters    
    
    # first current person
    let the read from stream source stream index = <my efficiency bonus generator index>;
    <read from stream "integer">;            
    let current efficiency bonus = <the read from stream result "integer">;
    let efficiency of helpers = value integer 0;
        
    <print <current person> "uses" <weapon of <current person>>>;
    
    <add "integer" <current efficiency bonus> <weapon bonus for <weapon of <current person>> against <weapon of <current guest>>>>;
    let current efficiency bonus = <the add result "integer">;        
            
    <add "integer" <strength of <current person>> <current efficiency bonus>>;
    let efficiency of <current person>=<the add result "integer">;
    
    <calculate the efficiency of the <current person> helpers against <current guest>>;    
    <add "integer" <efficiency of <current person>> <efficiency of helpers>>;
    let efficiency of <current person>=<the add result "integer">;
    
    
        
    
    # now current guest
    <read from stream "integer">; 
    let current efficiency bonus = <the read from stream result "integer">;
    let efficiency of helpers = value integer 0;
        
    <print <current guest> "uses" <weapon of <current guest>>>;
                
    <add "integer" <strength of <current guest>> <current efficiency bonus>>;
    let efficiency of <current guest>=<the add result "integer">;

    <calculate the efficiency of the <current guest> helpers against <current person>>;    
    <add "integer" <efficiency of <current guest>> <efficiency of helpers>>;
    let efficiency of <current guest>=<the add result "integer">;
    
    
    
    # now compare the efficiencies of both fighters
    <compare "integer" <efficiency of <current person>> <efficiency of <current guest>>>;
        
    <resolve the fight <the compare result>>;
                
    let decision of <current person>=value action no_action;
};

variable current person proposes current guest to exchange weapons (X:compare_result):code;
let variable current person proposes current guest to exchange weapons lower = value code {};
let variable current person proposes current guest to exchange weapons greater = value code {};
let variable current person proposes current guest to exchange weapons equal = value code 
{
    <print <current person> "proposes" <current guest> "to exchange weapons">;
    
    let <current guest> thinks that <current person> has proposed him to exchange weapons=value boolean true;
                        
    let decision of <current person>=value action no_action;
    
    <resolve the proposal to exchange weapons>;
};


variable current person accepts current guest proposal to exchange weapons (X:compare_result):code;
let current person accepts current guest proposal to exchange weapons lower = value code {};
let current person accepts current guest proposal to exchange weapons greater = value code {};
let current person accepts current guest proposal to exchange weapons equal = value code 
{
    <print <current person> "(" <weapon of <current person>> ") accepts" <current guest> "(" <weapon of <current guest>> ") proposal to exchange weapons">;
        
    # here we exchange the weapons
    let current weapon=<weapon of <current person>>;
    let weapon of <current person>=<weapon of <current guest>>;
    let weapon of <current guest>=<current weapon>;

    let variable <current person> thinks that <current guest> has proposed <current person> to exchange weapons=value boolean false;    
    
    <print <current person> "exchanges weapons with" <current guest>>;
    
    let variable decision of <current person>=value action no_action;
    
    <resolve the acceptance of the proposal to exchange weapons>;
};




###################



variable perform current person decision goto (A:place):code;
let perform current person decision goto (A:place)=value code
{
    let current decision = <decision of <current person>>;
    let current place = value place [(A:place)];
    let current action pattern = value action goto <current place>;
    
    #<print "check whether" <current person> "decision" <current decision> "equals" <current action pattern>>;
        
    <compare "action" <current decision> <current action pattern>>;
    <current person goto current place <the compare result>>;
};


variable perform current person decision tell (A:person) that (B:information):code;
let perform current person decision tell (A:person) that (B:information)=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current information = value place [(B:information)];
    let current action pattern = value action tell <current guest> that <current information>;

    #<print "check whether" <current person> "decision" <current decision> "equals" <current action pattern>>;    
    
    <compare "action" <current decision> <current action pattern>>;
    <current person tells current guest that current information <the compare result>>;
};

variable perform current person decision ask (A:person) to (B:action):code;
let perform current person decision ask (A:person) to (B:action)=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current action = value action [(B:action)];
    let current action pattern = value action ask <current guest> to <current action>;

    <compare "action" <current decision> <current action pattern>>;
    <current person asks current guest to current action <the compare result>>;    
};

variable perform current person decision ask (A:person) what is (B:person) function:code;
let perform current person decision ask (A:person) what is (B:person) function=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current guest 2 = value person [(B:person)];
    let current action pattern = value action ask <current guest> what is <current guest 2> function;
    
    <compare "action" <current decision> <current action pattern>>;
    <current person asks current guest what is current guest 2 function <the compare result>>;    
};

variable perform current person decision ask (A:person) what is (B:person) weapon:code;
let perform current person decision ask (A:person) what is (B:person) weapon=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current guest 2 = value person [(B:person)];    
    let current action pattern = value action ask <current guest> what is <current guest 2> weapon;
    
    <compare "action" <current decision> <current action pattern>>;
    <current person asks current guest what is current guest 2 weapon <the compare result>>;    
};

variable perform current person decision ask (A:person) whether (B:person) is a liar:code;
let perform current person decision ask (A:person) whether (B:person) is a liar=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current guest 2 = value person [(B:person)];    
    let current action pattern = value action ask <current guest> whether <current guest 2> is a liar;
    
    <compare "action" <current decision> <current action pattern>>;
    <current person asks current guest whether current guest 2 is a liar <the compare result>>;    
};


variable perform current person decision attack (A:person):code;
let perform current person decision attack (A:person)=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current action pattern = value action attack <current guest>;
    
    <compare "action" <current decision> <current action pattern>>;
    <current person attacks current guest <the compare result>>;    
};


variable perform current person decision propose (A:person) to exchange weapons:code;
let perform current person decision propose (A:person) to exchange weapons=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current action pattern = value action propose <current guest> to exchange weapons;
            
    <compare "action" <current decision> <current action pattern>>;
    <current person proposes current guest to exchange weapons <the compare result>>;    
};


variable perform current person decision accept (A:person) proposal to exchange weapons:code;
let perform current person decision accept (A:person) proposal to exchange weapons=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current action pattern = value action accept <current guest> proposal to exchange weapons;
    
    <for each person <current person> make an asking about weapon text>;
    <for each person <current guest> make an asking about weapon text>;

    <compare "action" <current decision> <current action pattern>>;
    <current person accepts current guest proposal to exchange weapons <the compare result>>;        
};

variable perform current person decision answer (A:person) whether (B:person) is a liar (ANSWER:boolean):code;
let perform current person decision answer (A:person) whether (B:person) is a liar (ANSWER:boolean)=value code
{
    let current decision = <decision of <current person>>;
    let current guest = value person [(A:person)];
    let current guest 2 = value person [(B:person)];
    let current answer = value boolean [(ANSWER:boolean)];    
    let current action pattern = value action answer <current guest> whether <current guest 2> is a liar <current answer>;
    
    <for each person <current person> make an asking about being a liar text>;
    <for each person <current guest> make an asking about being a liar text>;    
        
    <compare "action" <current decision> <current action pattern>>;
    <current person answers current guest whether current guest 2 is a liar <the compare result>>;
};

variable perform decision for (X:person):code;
let variable perform decision for (X:person)=value code
{
    let variable current person = value person [(X:person)];
    <print "perform decision for" <current person>>;
    
        
    <perform current person decision goto (A:place)>;
    <perform current person decision tell (A:person) that (B:information)>;
    <perform current person decision ask (A:person) to (B:action)>;
    <perform current person decision ask (A:person) what is (B:person) function>;
    <perform current person decision ask (A:person) what is (B:person) weapon>;
    <perform current person decision ask (A:person) whether (B:person) is a liar>;
    <perform current person decision answer (A:person) whether (B:person) is a liar (ANSWER:boolean)>;
    <perform current person decision attack (A:person)>;    
    <perform current person decision propose (A:person) to exchange weapons>;
    <perform current person decision accept (A:person) proposal to exchange weapons>;    
};


variable perform all the decisions:code;
let variable perform all the decisions=value code
{
    let Gotrek should be notified = value boolean false;
    let why should Gotrek be notified = value string "";
    <clear notification texts>;
    
    <print "performing all the decisions">;    
    <perform decision for (X:person)>;            
    
    <notify Gotrek if he should be notified <Gotrek should be notified>>;
};
